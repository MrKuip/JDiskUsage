
plugins {
  id 'java-library'
  id 'eclipse'
  id 'application'
  id 'org.openjfx.javafxplugin' version '0.1.0'
  id 'org.beryx.jlink' version '3.1.1'
}

defaultTasks 'jar'

test
{
  useJUnitPlatform()
  testLogging {
    showStandardStreams = true 
    afterTest { descriptor , result -> 
      logger.lifecycle("Test: " + descriptor.getClassName() + " " + descriptor.getName() + " -> " + result)
    }
  }
}

apply from: 'build-eclipse.gradle'

repositories {
  mavenCentral()
}

javafx {
    version = "24"
    modules = [ 'javafx.controls', 'javafx.web']
}

java {
  sourceCompatibility = JavaVersion.VERSION_23
  targetCompatibility = JavaVersion.VERSION_23
}

dependencies {
  implementation 'org.kku:fontIcons:1.0'
  implementation 'org.kku:commons-base:1.0.1'
  implementation 'org.kku:commons-fx:1.0.1'
  implementation 'com.miglayout:miglayout-javafx:11.4.2'
  implementation 'org.commonmark:commonmark:0.24.0'

  testImplementation 'org.junit.jupiter:junit-jupiter:5.13.1'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

application {
  mainClass.set('org.kku.jdiskusage.main.Main');
  mainModule.set('org.kku.jdiskusage');
}

println "parent in build.xml is ${gradle.parent?.rootProject?.rootDir}"

jlink {
  launcher {
    name = 'JDiskUsage'
  }

  jpackage {
    installerType = "deb"
  }
}

task initEclipseMetaData {
  doLast {
    createLauncher('Main', 'jdiskusage', 'org.kku.jdiskusage', 'org.kku.jdiskusage.main.Main')
  }
}


tasks.withType(JavaCompile) {
    options.deprecation = true
}

tasks.named("clean").configure {
    dependsOn gradle.includedBuilds*.task(":clean")
}

tasks.named("test").configure {
    dependsOn gradle.includedBuilds*.task(":test")
}

//gluonfx {
//   resourcesList = [ '.*properties$', '.*md$', '.*htm$', '.*html$' , '.*json$' ]
//}

